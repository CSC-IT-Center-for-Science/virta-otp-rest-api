buildscript {
    ext {
        springBootVersion = "1.4.0.RELEASE"
    }
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("pl.allegro.tech.build:axion-release-plugin:1.4.0")
    }
}

apply plugin: "java"
apply plugin: "spring-boot"
apply plugin: "jacoco"
apply plugin: "pl.allegro.tech.build.axion-release"

scmVersion {
    versionIncrementer "incrementMinor"
    tag {
        prefix = ""
        initialVersion = { tag, position -> "1.0.0" }
    }
}

project.version = scmVersion.version

processResources {
    filesMatching("application.properties") {
        expand(project.properties)
    }
}

bootRun {
    systemProperties System.properties
    systemProperties["app.version"] = project.version
}

springBoot {
    executable = true
}

jar {
    baseName = "opintotieto-api"
    version = project.version
}

compileJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

repositories {
    jcenter()
    mavenCentral()
    mavenLocal()
}

dependencies {
    compile("org.springframework.boot:spring-boot-starter-web")
    compile("org.springframework.boot:spring-boot-starter-data-jpa")
    compile("org.springframework.boot:spring-boot-starter-jdbc")
    compile("com.fasterxml.jackson.dataformat:jackson-dataformat-xml")
    compile("org.springframework.boot:spring-boot-devtools")
    compile("io.springfox:springfox-swagger2:2.5.0")
    compile("io.springfox:springfox-swagger-ui:2.5.0")
    compile("org.springframework.boot:spring-boot-starter-jersey")
    // MS JDBC driver is not in Maven central, install locally
    runtime("com.microsoft.sqlserver:sqljdbc4:4.0")
    runtime("com.h2database:h2")
    // Test
    testCompile("org.springframework.boot:spring-boot-starter-test")
    testCompile("com.google.code.gson:gson:2.7")
}

jacocoTestReport {
    reports {
        xml.enabled = false
        html.enabled = false
    }
}
